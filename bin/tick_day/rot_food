#!/usr/bin/env coffee

_ = require 'underscore'
async = require 'async'
db = require '../../db'
data = require '../../data'
commands = require '../../commands'
FOOD_ROT_CHANCE = .04

binomial = (trials, probability) ->
  successes = 0
  for i in [0..trials]
    successes++ if Math.random() < probability
  successes

replace_with_rotten_food = (character, food, count, cb) ->
  console.log "Rotting #{count} of #{character.name}'s #{food.plural}."
  commands.remove_item character, food, count, (err) ->
    return cb(err) if err?
    commands.add_item character, data.items.rotten_food, count, cb

check_for_rot = (character) ->
  (inventory_item, cb) ->
    item = data.items[inventory_item.item]
    return cb() unless _.contains(item.tags, 'food')
    amount_to_rot = binomial inventory_item.count, FOOD_ROT_CHANCE
    return cb() if amount_to_rot <= 0
    replace_with_rotten_food character, item, amount_to_rot, cb

async.series [
  (cb) ->
    db.characters.find().toArray (err, characters) ->
      return cb(err) if err?
      async.each characters, (character, cb) ->
        async.each character.items, check_for_rot(character), cb
      , cb
], (err) ->
  if err?
    console.log err
    process.exit 1
  else
    console.log 'Food Rotted'
    process.exit 0
